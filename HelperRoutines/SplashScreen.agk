
//                                                   _                _                           
//__      ____      ____      ____      _____  _ __ | | ___   _ _ __ (_)_  __  ___ ___  _ __ ___  
//\ \ /\ / /\ \ /\ / /\ \ /\ / /\ \ /\ / / _ \| '_ \| |/ / | | | '_ \| \ \/ / / __/ _ \| '_ ` _ \ 
// \ V  V /  \ V  V /  \ V  V /  \ V  V / (_) | | | |   <| |_| | |_) | |>  < | (_| (_) | | | | | |
//  \_/\_/    \_/\_/    \_/\_(_)  \_/\_/ \___/|_| |_|_|\_\\__, | .__/|_/_/\_(_)___\___/|_| |_| |_|
//                                                        |___/|_|                                
//
// Splash Screen
//
// (C) Jason Brooks 2015 All Rights Reserved
// www.wonkypix.com

#constant SPLASHSCREENEXPANDOUT	1.5
#constant SCREENWIDTH 1024
#constant SCREENHEIGHT 768

function drawSplashScreen(filename as String)
	
	// Remove all sprites/images loaded since first thing loaded is this
	DeleteAllSprites()
	DeleteAllImages()
//	DeleteAllObjects()			
	DeleteAllText()
	
	
	SetPrintSize(12)
	SetClearColor(0,0,0)
	SetBorderColor(0,0,0)
	
	
	// Load the Splash Screen Image for your Application
	splashScreen = CreateSprite(LoadImage(filename))
	
	width = GetSpriteWidth( splashScreen )
	height = GetSpriteHeight( splashScreen )
	
	posX = (SCREENWIDTH - width) * 0.5
	posY = (SCREENHEIGHT - height) * 0.5
	
	posX2 = (SCREENWIDTH -  (width  * SPLASHSCREENEXPANDOUT)) * 0.5	
	posY2 = (SCREENHEIGHT - (height * SPLASHSCREENEXPANDOUT)) * 0.5
	
	
	// Crude as you could center the image
	
	SetSpriteOffset(splashScreen, width * 0.5, height * 0.5)
	SetSpritePosition  (splashScreen,posX,posY)
	SetSpriteColorAlpha(splashScreen,0)
	

	
	// Create the Tween Sprites for FadeIn and Fade Out
	fadeIn  = CreateTweenSprite(1.0)	// Fade in  1 Second
	fadeOut = CreateTweenSprite(2.0)	// Fade Out 2 Seconds
	expandX  = CreateTweenSprite(2.0)	// Expand for 2 seconds
	expandY  = CreateTweenSprite(2.0)	// Expand for 2 seconds
	moveX	 = CreateTweenSprite(2.0)
	moveY	 = CreateTweenSprite(2.0)
	
	
	// Set the AlphaTweenSprites Values to fade in or out
	SetTweenSpriteAlpha(fadeIn,    0, 255, TweenEaseIn1())
	SetTweenSpriteAlpha(fadeOut, 255,   0, TweenEaseOut1())
	
	SetTweenSpriteSizeX(expandX, width,  width  * SPLASHSCREENEXPANDOUT, TweenEaseIn1())
	SetTweenSpriteSizeY(expandY, height, height * SPLASHSCREENEXPANDOUT, TweenEaseIn1())
	
	SetTweenSpriteX(moveX, posX, posX2, TweenEaseIn1())	
	SetTweenSpritey(moveY, posY, posY2, TweenEaseIn1())
	
	PlayTweenSprite(fadeIn, splashScreen,0)
	
	count# = Timer()
	
	// Crude but we will show the Splash Screen for 4 Seconds
	// Before fading out.  (total 6 seconds, 4 + 2)
	while (  (Timer() - Count#) < 4.0)
		UpdateAllTweens( getframetime() )
		sync()
	endwhile
		
	
	// Expand the sprite
	PlayTweenSprite(expandX, splashScreen, 0)
	PlayTweenSprite(expandY, splashScreen, 0)
	PlayTweenSprite(moveX,	 splashScreen, 0)
	PlayTweenSprite(moveY, 	 splashScreen, 0)
  
	// Play the FadeOutTween
	PlayTweenSprite(fadeOut, splashScreen,0)
	
	while ( GetTweenSpritePlaying(expandY, splashScreen ))
		UpdateAllTweens( getframetime() )
		sync()
	endwhile
	
	// Good Memory Management - Clean up after ourselves!
	DeleteTween(fadeIn)
	DeleteTween(FadeOut)
	DeleteTween(expandX)
	DeleteTween(expandY)
	DeleteTween(moveX)
	DeleteTween(moveY)
	
	DeleteImage(GetSpriteImageID(splashScreen))
	DeleteSprite(splashScreen)

endfunction

